grammar de.cau.cs.kieler.kvis.KVis with de.cau.cs.kieler.kexpressions.KExpressions

generate kvis "http://www.cau.de/cs/kieler/kvis/KVis"

import "http://www.eclipse.org/emf/2002/Ecore" as ecore
import "http://kieler.cs.cau.de/kexpressions/0.1.2" as kexpressions 

Visualization:
	'image' ':' image=STRING
	elements += Element*
;


Element:
	'animate' name=ID '{'
        (animations+=Animation)+
	'}'
;

Animation:
    'apply' type=ID ('using' variable=VariableReference)? '{'
        (attributeMappings+=AttributeMapping)*
    '}' ('when' condition=AndExpression)?
;

AttributeMapping:
    attribute=ID ':' (literal=AnyValue | (mappings+=Mapping) (',' mappings+=Mapping)*)
;

Mapping:
    variableDomain = VariableDomain 'is' attributeDomain = AttributeDomain
;

AndExpression returns Condition:
    Comparison 
    (({AndExpression.left=current} operator="and") right=Comparison)*
;

Comparison:
    left=VariableReference
    relation=CompareOperator
    (right=AnyValue | right=VariableReference)
;

VariableDomain returns Domain:
    value = AnyValue
    | range = Interval
;

Interval:
    (from=IntValue | from=FloatValue)
    Range
    (to=IntValue | to=FloatValue)
;

AttributeDomain returns Domain:
    VariableDomain
;

VariableReference:
    (model=ModelReference)?
    name=ID
    ('[' indices+=INT ']')*
;

ModelReference:
    name=ID '.'
;

AndOperator returns BooleanOperator:
    AND='and'
;

Range:
    '-' // Alternative would be '..'
;